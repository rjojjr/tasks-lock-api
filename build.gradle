plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.13'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'maven-publish'
    id 'java-library'
}

def build = '14'

group = 'rjojjr.com.github'
//version = "1.1.2.$build-SNAPSHOT"
version = "1.1.2-RELEASE"
sourceCompatibility = '17'


repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    // TODO - Add separate client build job that doesn't carry all this DB code bloat(?)
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    runtimeOnly 'com.mysql:mysql-connector-j'
}

java {
    withSourcesJar()
}

publish {
    dependsOn assemble
}

jar {
    enabled = true
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:3.0.13")
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username project.properties['username']
                password project.properties['password']
            }
            // change to point to your repo, e.g. http://my.org/repo
            url = 'http://192.168.1.149:8091/snapshots'
            allowInsecureProtocol = true
        }
    }
    publications {
        mavenPublish(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            groupId = 'rjojjr.com.github'
            artifactId = 'tasks-lock-api'
            version = version

            from components.java
        }

    }
}
